package CustomValue


/*
包名：wurst版——逆天自定义值
作者：FBI工坊
QQ：750160517
*/
let rootTable=InitHashtable()
let playerTable=InitHashtable()
hashtable array playerTables

//base
public function T1.setCustomV<T1,T2>(hashtable table,int valName,T2 anyprop)
    table.saveInt(this castTo int, valName, anyprop castTo int)
public function T1.setCustomVR<T1>(hashtable table,int valName,real anyprop)
    table.saveReal(this castTo int, valName, anyprop)
public function T1.getCustomV<T1,T2>(hashtable table,int valName) returns T2
    return table.loadInt(this castTo int, valName) castTo T2
public function T1.getCustomVR<T1>(hashtable table,int valName) returns real
    return table.loadReal(this castTo int, valName)
public function T1.hasCustomV<T1,T2>(hashtable table,int valName) returns bool
    return table.hasInt(this castTo int, valName)
public function T1.hasCustomVR<T1>(hashtable table,int valName) returns bool
    return table.hasReal(this castTo int, valName)

//设置自定义值
@inline public function T1.setCustomV<T1,T2>(string valName,T2 anyprop)
    this.setCustomV(valName.getHash(), anyprop)
public function T1.setCustomV<T1,T2>(int valName,T2 anyprop)
    this.setCustomV(rootTable,valName, anyprop)
public function T1.setCustomVR<T1>(int valName,real anyprop)
    this.setCustomVR(rootTable,valName, anyprop)
/** 玩家——存自定义值*/
public function player.setCVR(int valName,real anyprop)
    let pid=this.getId()+100
    pid.setCustomVR(playerTable,valName, anyprop)
public function player.setCV<T2>(int valName,T2 anyprop)
    this.setCustomV(playerTable,valName, anyprop)
public function player.setCV<T2>(string valName,T2 anyprop)
    this.setCV(valName.getHash(), anyprop)
//获取自定义值
public function T1.getCustomV<T1,T2>(string valName) returns T2
    return this.getCustomV<T1,T2>(valName.getHash())
public function T1.getCustomV<T1,T2>(int valName) returns T2
    return this.getCustomV<T1,T2>(rootTable, valName)
public function T1.getCustomVR<T1>(int valName) returns real
    return this.getCustomVR<T1>(rootTable, valName)
/** 玩家——获取自定义值*/
public function player.getCV<T2>(int valName) returns T2
    return this.getCustomV<player,T2>(playerTable, valName)
public function player.getCV<T2>(string valName) returns T2
    return this.getCV<T2>(valName.getHash())
public function player.getCVR(int valName) returns real
    let pid=this.getId()+100
    return pid.getCustomVR<player>(playerTable, valName)

//是否拥有自定义值
public function T1.hasCustomV<T1,T2>(string valName) returns bool
    return this.hasCustomV<T1,T2>(valName.getHash())
public function T1.hasCustomV<T1,T2>(int valName) returns bool
    return this.hasCustomV<T1,T2>(rootTable, valName)
public function T1.hasCustomVR<T1>(int valName) returns bool
    return this.hasCustomVR<T1>(rootTable, valName)

public function player.hasCV<T2>(int valName) returns bool
    return this.hasCustomV<player,T2>(playerTable, valName)
public function player.hasCVR(int valName) returns bool
    let pid=this.getId()+100
    return pid.hasCustomVR<player>(playerTable, valName)
//自定义值加法——————————————
// public function T1.addCustomV<T1>(string valName,int anyprop)
//     this.setCustomV(valName,this.getCustomV<thistype,int>(valName)+anyprop)
// public function T1.addCustomVR<T1>(string valName,real anyprop)
//     this.setCustomVR(valName,this.getCustomVR<thistype>(valName)+anyprop)
public function T1.addCustomV<T1>(int valName,int anyprop)
    this.setCustomV(valName,this.getCustomV<thistype,int>(valName)+anyprop)
public function T1.addCustomVR<T1>(int valName,real anyprop)
    this.setCustomVR(valName,this.getCustomVR<thistype>(valName)+anyprop)
/** 玩家——存自定义值*/
public function player.addCV(int valName,int anyprop)
    this.setCV<int>(valName,this.getCV<int>(valName)+anyprop)
public function player.addCVR(int valName,real anyprop)
    this.setCVR(valName,this.getCVR(valName)+anyprop)
//清除该泛型绑定的所有自定义值
public function T1.clearCustomV<T1>()
    rootTable.flushChild(this castTo int)

//为玩家设置该id的值
public function player.setIdCV<T1>(int id,string tname,T1 val)
    this.setIdCV<T1>(id, tname.getHash(), val)
public function player.getIdCV<T1>(int id,string tname) returns T1
    return this.getIdCV<T1>(id, tname.getHash())
public function player.setIdCV<T1>(int id,int tname,T1 val)
    let pid=this.getId()
    if playerTables[pid]==null
        // printDebug("没有玩家表，创建ht。")
        playerTables[pid]=InitHashtable()
    id.setCustomV(playerTables[pid],tname, val)
public function player.getIdCV<T1>(int id,int tname) returns T1
    let pid=this.getId()
    if playerTables[pid]==null
        // printDebug("没有玩家表，创建ht。")
        playerTables[pid]=InitHashtable()
    return id.getCustomV<int,T1>(playerTables[pid], tname)


init
    playerTables[0]=InitHashtable()
    playerTables[1]=InitHashtable()
    playerTables[2]=InitHashtable()
    playerTables[3]=InitHashtable()
@test function rootTableTest()
    let i1=100
    let i2="今天天气"
    let i3=createUnit(Player(0), 'hfoo', vec2(0, 0), angle(0))
    i1.setCustomV("自定义值", "我我我")
    i2.setCustomV("自定义值2", "下雨")
    i3.setCustomV("攻击力", 1024)
    print(i1.getCustomV<int,string>("自定义值"))
    print(i2.getCustomV<string,string>("自定义值2"))
    if i3.hasCustomV<unit,int>("攻击力")
        print(i3.getCustomV<unit,int>("攻击力"))
        i3.addCustomV("攻击力".getHash(), 1024)
        print(i3.getCustomV<unit,int>("攻击力"))
    else
        print("没有")